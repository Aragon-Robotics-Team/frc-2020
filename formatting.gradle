// // Required in build.gradle:
//
// plugins {
//     id "com.diffplug.gradle.spotless" version "3.26.0" apply false
// }
//
// apply from: file("formatting.gradle")


apply plugin: "java"
apply plugin: "com.diffplug.gradle.spotless"

spotless {
    lineEndings = "windows"

    format 'misc', {
        target '**.*'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        target 'src/main/java/frc/**/*.java'

        removeUnusedImports()
        replaceRegex "Star Import", "import.*\\*;\\n", ""
        replace "Multiple Semicolon", ";;", ";"
        replaceRegex "Whitespace after {", "\\{[\\s]*", "{\n"
        replaceRegex "Whitespace before }", "[\\s]*\\}", "\n}"

        // Use this only for organize imports
        // Will be replaced with eclipse format file
        googleJavaFormat()

        eclipseFormatFile 'gradle/eclipse-style.xml'
    }
}

compileJava.dependsOn 'spotlessApply'
